	page	0
	cpu	z80

;	PALO ALTO TINY BASIC
;	PORTABLE Z80 VERSION
;	TARGET: SBCZ80
;	ASSEMBLER: ARCPIT XZ80.EXE
;
;	MEMORY ASIGN
SuperMEZ80 = 0
SMEZ80Full = 0
RAM12K = 0
RAM8K = 1
RAM4K = 0

	IF	SuperMEZ80
RAMTOP	EQU	end_code
RAMSIZ	EQU	10000H - end_code
VALTOP	EQU	10000H - 300H
TSTACK	EQU	10000H - 180H
	ENDIF

	IF	SMEZ80Full
RAMTOP	EQU	08000H
RAMSIZ	EQU	8000H
VALTOP	EQU	RAMTOP + RAMSIZ - 300H
TSTACK	EQU	RAMTOP + RAMSIZ - 180H
	ENDIF

	IF	RAM12K
RAMTOP	EQU	0C000H
RAMSIZ	EQU	3000H
VALTOP	EQU	RAMTOP + RAMSIZ - 300H
TSTACK	EQU	RAMTOP + RAMSIZ - 180H
	ENDIF

	IF	RAM8K
RAMTOP	EQU	08000H
RAMSIZ	EQU	2000H
VALTOP	EQU	RAMTOP + RAMSIZ - 300H
TSTACK	EQU	RAMTOP + RAMSIZ - 180H
	ENDIF

	IF	RAM4K
; ORIGINAL MEMORY MAP for PIC18F47Q43
RAMTOP	EQU	08000H
RAMSIZ	EQU	1000H
VALTOP	EQU	RAMTOP + RAMSIZ - 300H
TSTACK	EQU	RAMTOP + RAMSIZ - 180H
	ENDIF

	if SuperMEZ80
ZTBTOP	EQU	40H
	else
ZTBTOP	EQU	4C00H
	endif

;;
;	PALO ALTO TINY BASIC
;
LBUF	EQU	VALTOP+0037H
LBFSZ	EQU	80
MSTK	EQU	LBUF+LBFSZ
TMSTK	EQU	MSTK+30
;
PROMPT	EQU	'>'
RUBOUT	EQU	7FH
BS	EQU	08H
ESC	EQU	1BH
;
MTST	MACRO	p1, p2
	CALL	TEST
	DB	p1
	DB	p2-$-1
	ENDM
CASE_	MACRO	p1, p2
	DB	p1
	DB	(p2 >> 8)+80H
	DB	p2 & 0FFH
	ENDM
MENDC	MACRO	p1
	DB	(p1 + 8000H) >> 8
	DB	p1 & 0FFH
	ENDM
;

	org	ZTBTOP

ITOP	EQU	$

	jp	ENTRY		;COLD START
	jp	START		;WARM START

ENTRY:	LD	SP,TSTACK
	LD	HL,OTOP
	LD	(OBTM),HL
	CALL	CRLF
	LD	DE,OPNMS1
	SUB	A
	CALL	MSG
	LD	DE,OPNMS2
	SUB	A
	CALL	MSG
	LD	DE,OPNMS3
	SUB	A
	CALL	MSG
;
START:	LD	SP,TSTACK
	CALL	CRLF
	LD	DE,OKMES
	SUB	A
	CALL	MSG
	LD	HL,$+7
	LD	(CLABL),HL
	LD	HL,0000H
	LD	(FCNTR),HL
	LD	(RSTCK),HL
;
EDITR:	LD	A,PROMPT
	CALL	GETL
	PUSH	DE
	LD	DE,LBUF
	CALL	GINT
	CALL	SKPBL
	LD	A,H
	OR	L
	POP	BC
	JP	Z,KWCPR
INSRT:	DEC	DE
	LD	A,H
	LD	(DE),A
	DEC	DE
	LD	A,L
	LD	(DE),A
	PUSH	BC
	PUSH	DE
	LD	A,C
	SUB	E
	PUSH	AF
	CALL	SRCH
	PUSH	DE
	JP	NZ,MOVE
	PUSH	DE
	CALL	SKIPL
	POP	BC
	LD	HL,(OBTM)
	CALL	TRNSF
	LD	H,B
	LD	L,C
	LD	(OBTM),HL
MOVE:	POP	BC
	LD	HL,(OBTM)
	POP	AF
	PUSH	HL
	CP	03H
	JP	Z,START
	ADD	A,L
	LD	L,A
	LD	A,00H
	ADC	A,H
	LD	H,A
	LD	DE,VALTOP
	CALL	COMPR
	JP	NC,ERSRY
	LD	(OBTM),HL
	POP	DE
	CALL	TR2
	POP	DE
	POP	HL
	CALL	TRNSF
	JP	EDITR
;
GETL:	CALL	PUT
	LD	DE,LBUF
GETL1:	CALL	GET
	JP	Z,GETL1
	CP	0AH
	JP	Z,GETL1
	OR	A
	JP	Z,GETL1
	CP	BS
	JP	Z,CRUB
	CP	'X'-'@'
	JP	Z,CCAN
	CALL	PUT

	call	GET1	; LOW to UPPER

	LD	(DE),A
	INC	DE
	CP	0DH
	RET	Z
	LD	A,E
	CP	LBUFL + LBFSZ
	JP	NZ,GETL1
CRUB:	LD	A,E
	CP	LBUFL
	JP	Z,GETL1
	DEC	DE
	LD	A,BS
	CALL	PUT

	LD	A,' '
	CALL	PUT
	LD	A,BS
	CALL	PUT

	JP	GETL1
CCAN:	CALL	CRLF
	LD	A,'@'
	JP	GETL
LBUFL	EQU	LBUF & 0FFH
;
SRCH:	LD	A,H
	OR	A
	JP	M,ERHOW
	LD	DE,OTOP
SRCH1:	PUSH	HL
	LD	HL,(OBTM)
	DEC	HL
	CALL	COMPR
	POP	HL
	RET	C
	LD	A,(DE)
	SUB	L
	LD	B,A
	INC	DE
	LD	A,(DE)
	SBC	A,H
	JP	C,SKPL1
	DEC	DE
	OR	B
	RET
;
SKIPL:	INC	DE
SKPL1:	INC	DE
SKPL2:	LD	A,(DE)
	CP	0DH
	JP	NZ,SKPL1
	INC	DE
	JP	SRCH1
;
TRNSF:	CALL	COMPR
	RET	Z
	LD	A,(DE)
	LD	(BC),A
	INC	DE
	INC	BC
	JP	TRNSF
;
TR2:	LD	A,B
	SUB	D
	JP	NZ,TR2E
	LD	A,C
	SUB	E
	RET	Z
TR2E:	DEC	DE
	DEC	HL
	LD	A,(DE)
	LD	(HL),A
	JP	TR2
;
KWCPR:	LD	HL,CMDKW-1
;
NXTKW:	CALL	SKPBL
NXTK1:	PUSH	DE
KWC1:	LD	A,(DE)
	INC	DE
	CP	'.'
	JP	Z,KWSRT
	INC	HL
	CP	(HL)
	JP	Z,KWC1
	LD	A,7FH
	DEC	DE
	CP	(HL)
	JP	C,EXEQT
KWSK1:	INC	HL
	CP	(HL)
	JP	NC,KWSK1
	INC	HL
	POP	DE
	JP	NXTK1
;
KWSRT:	LD	A,7FH
KWSK2:	INC	HL
	CP	(HL)
	JP	NC,KWSK2
;
EXEQT:	LD	A,(HL)
	INC	HL
	LD	L,(HL)
	AND	7FH
	LD	H,A
	POP	AF
	JP	(HL)
;
TEST:	EX	(SP),HL
	CALL	SKPBL
	CP	(HL)
	INC	HL
	JP	Z,TSTEQ
	PUSH	BC
	LD	C,(HL)
	LD	B,00H
	ADD	HL,BC
	POP	BC
	DEC	DE
TSTEQ:	INC	DE
	INC	HL
	EX	(SP),HL
	RET
;
SKPBL:	LD	A,(DE)
	CP	' '
	RET	NZ
	INC	DE
	JP	SKPBL
;
CMDKW:	CASE_	"LIST", LIST
	CASE_	"RUN", RUN
	CASE_	"NEW", NEW
	CASE_	"BYE", BYE
STMKW:	CASE_	"NEXT", NEXT
	CASE_	"LET", LET
	CASE_	"INPUT", INPUT
	CASE_	"IF", IFSTM
	CASE_	"GOTO", GOTO
	CASE_	"GOSUB", GOSUB
	CASE_	"RETURN", RETRN
	CASE_	"REM", REM
	CASE_	"FOR", FOR
	CASE_	"PRINT", PRINT
	CASE_	"STOP", STOP
	MENDC	LETDF
;
BYE:	ld	c, 1
	rst	30H		; return to EMUZ80-MON
	
NEW:	CALL	TSCR2
	LD	HL,OTOP
	LD	(OBTM),HL
STOP:	CALL	TSCR2
	JP	START
;
GOSUB:	CALL	PSHV
	CALL	EEXPR
	PUSH	DE
	CALL	SRCH
	JP	NZ,ERHW1
	LD	HL,(CLABL)
	PUSH	HL
	LD	HL,(RSTCK)
	PUSH	HL
	LD	HL,0000H
	LD	(FCNTR),HL
	ADD	HL,SP
	LD	(RSTCK),HL
	JP	RUN2

RETRN:	CALL	TSCR2
	LD	HL,(RSTCK)
	LD	A,H
	OR	L
	JP	Z,ERWHT
	LD	SP,HL
	POP	HL
	LD	(RSTCK),HL
	POP	HL
	LD	(CLABL),HL
	POP	DE
	CALL	POPV
	JP	ENDL
;
FOR:	CALL	PSHV
	CALL	LTSUB
	DEC	HL
	LD	(FCNTR),HL
	LD	HL,KWTO-1
	JP	NXTKW
KWTO:	CASE_	"TO", FORTO
	MENDC	ERWHT
;
FORTO:	CALL	EEXPR
	LD	(FTOV),HL
	LD	HL,KWSTP-1
	JP	NXTKW
KWSTP:	CASE_	"STEP" ,FSTEP
	MENDC	FSTP1
;
FSTEP:	CALL	EEXPR
	JP	FOR0
FSTP1:	LD	HL,0001H
FOR0:	LD	(FSTPV),HL
	LD	HL,(CLABL)
	LD	(FLABL),HL
	EX	DE,HL
	LD	(FOBJ),HL
	LD	BC,000AH
	LD	HL,(FCNTR)
	EX	DE,HL
	LD	H,B
	LD	L,B
	ADD	HL,SP
	DB	3EH
FOR3:	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	OR	(HL)
	JP	Z,FOR10
	LD	A,(HL)
	DEC	HL
	CP	D
	JP	NZ,FOR3
	LD	A,(HL)
	CP	E
	JP	NZ,FOR3
FOR5:	EX	DE,HL
	LD	HL,0000H
	ADD	HL,SP
	LD	B,H
	LD	C,L
	LD	HL,000AH
	ADD	HL,DE
	CALL	TR2
	LD	SP,HL
FOR10:	LD	HL,(FOBJ)
	EX	DE,HL
	JP	ENDL
;
NEXT:	CALL	TSTV
	JP	C,ERWHT
	LD	(NCNTR),HL
NEXT1:	PUSH	DE
	EX	DE,HL
	LD	HL,(FCNTR)
	LD	A,H
	OR	L
	JP	Z,ERWT1
	CALL	COMPR
	JP	Z,NEXT2
	POP	DE
	CALL	POPV
	LD	HL,(NCNTR)
	JP	NEXT1
NEXT2:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,(FSTPV)
	PUSH	HL
	ADD	HL,DE
	EX	DE,HL
	LD	HL,(FCNTR)
	LD	(HL),E
	INC	HL
	LD	(HL),D
	LD	HL,(FTOV)
	POP	AF
	OR	A
	JP	P,NEXT4
	EX	DE,HL
NEXT4:	CALL	CMINT
	POP	DE
	JP	C,NEXT5
	LD	HL,(FLABL)
	LD	(CLABL),HL
	LD	HL,(FOBJ)
	EX	DE,HL
	JP	ENDL
NEXT5:	CALL	POPV
	JP	ENDL
;
PSHV:	LD	HL,TMSTK
	CALL	TWSCP
	POP	BC
	ADD	HL,SP
	JP	NC,ERSRY
	LD	HL,(FCNTR)
	LD	A,H
	OR	L
	JP	Z,NPSH
	LD	HL,(FOBJ)
	PUSH	HL
	LD	HL,(FLABL)
	PUSH	HL
	LD	HL,(FTOV)
	PUSH	HL
	LD	HL,(FSTPV)
	PUSH	HL
	LD	HL,(FCNTR)
NPSH:	PUSH	HL
	PUSH	BC
	RET
;
POPV:	POP	BC
	POP	HL
	LD	(FCNTR),HL
	LD	A,H
	OR	L
	JP	Z,NPOP
	POP	HL
	LD	(FSTPV),HL
	POP	HL
	LD	(FTOV),HL
	POP	HL
	LD	(FLABL),HL
	POP	HL
	LD	(FOBJ),HL
NPOP:	PUSH	BC
	RET
;
IFSTM:	CALL	EEXPR
	LD	A,H
	OR	L
	JP	NZ,NXTGO
REM:	LD	HL,0000H
	CALL	SKPL2
	JP	NC,RUN2
	JP	START
;
LIST:	CALL	GINT
	CALL	TSCR2
	CALL	SRCH
LISTL:	JP	C,START
	CALL	WLINE
	CALL	BREAK
	CALL	SRCH1
	JP	LISTL
;
WLINE:	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	INC	DE
	LD	C,04H
	CALL	WINT
	LD	A,' '
	CALL	PUT
	SUB	A
	CALL	MSG
	RET
;
PRINT:	LD	C,6
PRSC:	MTST	3BH,PRCR
	CALL	CRLF
	JP	NXTGO
PRCR:	MTST	0DH,PRSHA
	CALL	CRLF
	JP	RUN1
PRSHA:	MTST	23H,PRLTR
	CALL	EEXPR
	LD	C,L
	JP	PRCMA
PRLTR:	CALL	PRSUB
	JP	PREXP
PRCMA:	MTST	2CH,PREND
	CALL	TSTSC
	JP	PRSHA
PREND:	CALL	CRLF
	JP	ENDL
PREXP:	CALL	EEXPR
	PUSH	BC
	CALL	WINT
	POP	BC
	JP	PRCMA
;
PRSUB:	MTST	22H,PR13
	LD	A,22H
	JP	PR11
PR13:	MTST	27H,PR14
	LD	A,27H
PR11:	CALL	MSG
	CP	0DH
	POP	HL
	JP	Z,RUN1
	JP	PR12
PR14:	MTST	5FH,PR15
	LD	A,80H+0DH
	CALL	PUT
	CALL	PUT
	POP	HL
PR12:	INC	HL
	INC	HL
	INC	HL
	JP	(HL)
PR15:	RET
;
WINT:	PUSH	DE
	LD	DE,000AH
	PUSH	DE
	LD	B,D
	DEC	C
	CALL	ABS
	JP	P,WINT1
	LD	B,'-'
	DEC	C
WINT1:	PUSH	BC
WINT2:	CALL	DIVID
	LD	A,B
	OR	C
	JP	Z,WINT3
	EX	(SP),HL
	DEC	L
	PUSH	HL
	LD	H,B
	LD	L,C
	JP	WINT2
WINT3:	POP	BC
WINT4:	DEC	C
	LD	A,C
	OR	A
	JP	M,WINT5
	LD	A,' '
	CALL	PUT
	JP	WINT4
WINT5:	LD	A,B
	CALL	PUT
	LD	E,L
WINT6:	LD	A,E
	CP	0AH
	POP	DE
	RET	Z
	ADD	A,30H
	CALL	PUT
	JP	WINT6
;
ERRIN:	LD	HL,(NCNTR)
	LD	SP,HL
	POP	HL
	LD	(CLABL),HL
	POP	DE
	POP	DE
INPUT:	PUSH	DE
	CALL	PRSUB
	JP	INPT2
INPT1:	CALL	TSTV
	JP	C,INPT6
	JP	INPT4
INPT2:	PUSH	DE
	CALL	TSTV
	JP	C,ERWHT
	LD	A,(DE)
	LD	C,A
	SUB	A
	LD	(DE),A
	POP	DE
	CALL	MSG
	LD	A,C
	DEC	DE
	LD	(DE),A
INPT4:	PUSH	DE
	EX	DE,HL
	LD	HL,(CLABL)
	PUSH	HL
	LD	HL,INPUT
	LD	(CLABL),HL
	LD	HL,0000H
	ADD	HL,SP
	LD	(NCNTR),HL
	PUSH	DE
	LD	A,':'
	CALL	GETL
	LD	DE,LBUF
	CALL	EEXPR
	POP	DE
	EX	DE,HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	POP	HL
	LD	(CLABL),HL
	POP	DE
INPT6:	POP	AF
	MTST	2CH,ENDL
	JP	INPUT
;
LETDF:	CALL	TSCR1
	CP	0DH
	JP	Z,ENDL
LET:	CALL	LTSUB
	MTST	2CH,ENDL
	JP	LET
;
LTSUB:	CALL	TSTV
	JP	C,ERWHT
	PUSH	HL
	MTST	3DH,LERR
	CALL	EEXPR
	LD	B,H
	LD	C,L
	POP	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B
	RET
;
GOTO:	CALL	EEXPR
	PUSH	DE
	CALL	TSCR2
	CALL	SRCH
	JP	NZ,ERHW1
	POP	AF
	JP	RUN2
;
RUN:	CALL	TSCR2
	LD	DE,OTOP
RUN1:	LD	HL,0000H
	CALL	SRCH1
	JP	C,START
RUN2:	EX	DE,HL
	LD	(CLABL),HL
	EX	DE,HL
	INC	DE
	INC	DE
NXTGO:	CALL	BREAK
	LD	HL,STMKW-1
	JP	NXTKW
;
ENDL:	CALL	TSTSC
LERR:	JP	ERWHT
;
TSTSC:	MTST	3BH,TSCR1
	POP	AF
	JP	NXTGO
;
TSCR1:	MTST	0DH,TSRTN
	POP	AF
	JP	RUN1
TSRTN:	RET
;
TSCR2:	CALL	SKPBL
	CP	0DH
	RET	Z
	JP	ERWHT
;
EEXPR:	CALL	EXPR
	PUSH	HL
	LD	HL,ROPKW-1
	JP	NXTKW
ROPKW:	CASE_	">=", GE1
	CASE_	"<>", NE1
	CASE_	'>', GT1
	CASE_	'=', EQ1
	CASE_	"<=", LE1
	CASE_	'<', LT1
	MENDC	NOROP
;
GE1:	CALL	IFEXQ
	RET	C
	LD	L,A
	RET
;
NE1:	CALL	IFEXQ
	RET	Z
	LD	L,A
	RET
;
GT1:	CALL	IFEXQ
	RET	Z
	RET	C
	LD	L,A
	RET
LE1:	CALL	IFEXQ
	LD	L,A
	RET	Z
	RET	C
	LD	L,H
	RET
EQ1:	CALL	IFEXQ
	RET	NZ
	LD	L,A
	RET
LT1:	CALL	IFEXQ
	RET	NC
	LD	L,A
	RET
NOROP:	POP	HL
	RET
;
IFEXQ:	LD	A,C
	POP	HL
	POP	BC
	PUSH	HL
	PUSH	BC
	LD	C,A
	CALL	EXPR
	EX	DE,HL
	EX	(SP),HL
	CALL	CMINT
	POP	DE
	LD	HL,0000H
	LD	A,01H
	RET
;
EXPR:	MTST	2DH,EXPR1
	LD	HL,0000H
	JP	NEGA1
EXPR1:	MTST	2BH,EXPR3
EXPR3:	CALL	TERM
EXPR2:	MTST	2BH,NEGA0
	PUSH	HL
	CALL	TERM
	JP	ADDBL
NEGA0:	MTST	2DH,EXPRT
NEGA1:	PUSH	HL
	CALL	TERM
	CALL	TWSCP
ADDBL:	EX	DE,HL
	EX	(SP),HL
	LD	A,H
	XOR	D
	LD	A,D
	ADD	HL,DE
	POP	DE
	JP	M,EXPR2
	XOR	H
	JP	P,EXPR2
	JP	ERHOW
;
TERM:	CALL	FACTR
MULT:	MTST	2AH,DIV
	PUSH	HL
	CALL	FACTR
	LD	B,00H
	CALL	ABS
	EX	DE,HL
	EX	(SP),HL
	CALL	ABS
	LD	A,H
	OR	A
	JP	Z,MULT1
	LD	A,D
	OR	D
	EX	DE,HL
	JP	NZ,ERHW1
MULT1:	LD	A,L
	LD	L,0
	OR	A
	JP	Z,TERM1
MULTL:	ADD	HL,DE
	JP	C,ERHW1
	DEC	A
	JP	NZ,MULTL
	JP	TERM1
DIV:	MTST	2FH,EXPRT
	PUSH	HL
	CALL	FACTR
	LD	B,00H
	CALL	ABS
	EX	DE,HL
	EX	(SP),HL
	CALL	ABS
	LD	A,D
	OR	E
	JP	Z,ERHW1
	PUSH	BC
	CALL	DIVID
	LD	H,B
	LD	L,C
	POP	BC
TERM1:	POP	DE
	LD	A,H
	OR	A
	JP	M,ERHOW
	LD	A,B
	OR	A
	CALL	M,TWSCP
	JP	MULT
;
DIVID:	PUSH	HL
	LD	L,H
	LD	H,00H
	CALL	DIVSB
	LD	B,C
	LD	A,L
	POP	HL
	LD	H,A
DIVSB:	LD	C,0FFH
DIVS1:	INC	C
	CALL	DIFF
	JP	NC,DIVS1
	ADD	HL,DE
EXPRT:	RET
;
DIFF:	LD	A,L
	SUB	E
	LD	L,A
	LD	A,H
	SBC	A,D
	LD	H,A
	RET
;
ABS:	LD	A,H
	OR	A
	RET	P
TWSCP:	LD	A,H
	CPL
	LD	H,A
	LD	A,L
	CPL
	LD	L,A
	INC	HL
	LD	A,B
	XOR	80H
	LD	B,A
	RET
;
CMINT:	LD	A,H
	XOR	D
	JP	P,COMPR
	EX	DE,HL
COMPR:	LD	A,H
	CP	D
	RET	NZ
	LD	A,L
	CP	E
	RET
;
FACTR:	LD	HL,FNKW-1
	JP	NXTKW
FNKW:	CASE_	"RND", RND
	CASE_	"ABS", FNABS
	CASE_	"SIZE", RSIZE
	MENDC	VAR
;
RND:	CALL	EXPR0
	LD	A,H
	OR	A
	JP	M,ERHOW
	OR	L
	JP	Z,ERHOW
	PUSH	DE
	PUSH	HL
	LD	HL,(RWRK)
;;	INC	HL
	LD	A,H
	AND	07H
	LD	H,A
;;	DEC	L
	LD	DE,ITOP	;
	ADD	HL,DE	;
RND1:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(RWRK),HL
	POP	HL
	EX	DE,HL
	PUSH	BC
	CALL	DIVID
	POP	BC
	POP	DE
	INC	HL
	RET
;
FNABS:	CALL	EXPR0
	CALL	ABS
	LD	A,H
	OR	H
	JP	M,ERHOW
	RET
;
RSIZE:	LD	HL,(OBTM)
	PUSH	DE
	EX	DE,HL
	LD	HL,VALTOP
	CALL	DIFF
	POP	DE
	RET
;
VAR:	CALL	TSTV
	JP	C,NUMB
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	RET
;
TSTV:	CALL	SKPBL
	SUB	'@'
	RET	C
	JP	NZ,TSTV1
	INC	DE
	CALL	EXPR0
	ADD	HL,HL
	JP	C,ERHOW
	PUSH	DE
	EX	DE,HL
	CALL	RSIZE
	CALL	COMPR
	JP	C,ERSY1
	LD	HL,VALTOP
	CALL	DIFF
	POP	DE
	RET
TSTV1:	CP	'Z'+1-'@'
	CCF
	RET	C
	INC	DE
	LD	HL,VALTOP
	RLCA
	ADD	A,L
	LD	L,A
	LD	A,00H
	ADC	A,H
	LD	H,A
	RET
;
GINT:	LD	HL,0000H
	LD	B,H
	CALL	SKPBL
GETI1:	CP	'0'
	RET	C
	CP	'9'+1
	RET	NC
	LD	A,0F0H
	AND	H
	JP	NZ,ERHOW
	INC	B
	PUSH	BC
	LD	B,H
	LD	C,L
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,BC
	ADD	HL,HL
	LD	A,(DE)
	INC	DE
	AND	0FH
	ADD	A,L
	LD	L,A
	LD	A,00H
	ADC	A,H
	LD	H,A
	POP	BC
	LD	A,(DE)
	JP	M,ERHOW
	JP	GETI1
;
NUMB:	CALL	GINT
	LD	A,B
	OR	A
	RET	NZ
;
EXPR0:	MTST	28H,ERWHT
	CALL	EEXPR
	MTST	29H,ERWHT
	RET
;
ERWHT:	PUSH	DE
ERWT1:	LD	DE,WHTMS
	JP	LERMS
ERHOW:	PUSH	DE
ERHW1:	LD	DE,HOWMS
	JP	LERMS
ERSRY:	PUSH	DE
ERSY1:	LD	DE,SRYMS
LERMS:	SUB	A
	CALL	MSG
	POP	DE
	LD	A,(DE)
	PUSH	AF
	SUB	A
	LD	(DE),A
	LD	HL,(CLABL)
	PUSH	HL
	LD	A,(HL)
	INC	HL
	OR	(HL)
	POP	DE
	JP	Z,START
	LD	A,(HL)
	OR	A
	JP	M,ERRIN
	CALL	WLINE
	DEC	DE
	POP	AF
	LD	(DE),A
	LD	A,'?'
	CALL	PUT
	SUB	A
	CALL	MSG
	JP	START
;
OKMES:	DB	'OK'
	DB	0DH
HOWMS:	DB	'HOW?'
	DB	0DH
WHTMS:	DB	'WHAT?'
	DB	0DH
SRYMS:	DB	'SORRY'
	DB	0DH
;
OPNMS1:	DB	'PALO ALTO '
	DB	'TINY BASIC'
	DB	0DH
OPNMS2:	DB	'PORTABLE '
	DB	'Z80 VERSION'
	DB	0DH
OPNMS3:	DB	'SBCZ80 EDITION'
	DB	0DH
;
MSG:	LD	B,A
MSG1:	LD	A,(DE)
	INC	DE
	CP	B
	RET	Z
	CALL	PUT
	CP	0DH
	JP	NZ,MSG1
	RET
;
CRLF:	LD	A,0DH
PUT:	RST	08H
;
	CP	0DH
	RET	NZ
	LD	A,0AH
	CALL	PUT
	LD	A,0DH
	RET
;
BREAK	EQU	$
GET:	RST	18H
	RET	Z
	RST	10H
	CP	ESC
	ret	nz

;	JP	NZ,GET1

;	INC	SP
;	INC	SP
	JP	START
;
GET1:	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	AND	11011111B
	RET
;
	db	($ & 0FF00H)+100H-$ dup(0FFH)

	if SuperMEZ80
end_code:
	else
	ORG	RAMTOP
	endif
;
OBTM:	DS	0002H
CLABL:	DS	0002H
RSTCK:	DS	0002H
NCNTR:	DS	0002H
FCNTR:	DS	0002H
FSTPV:	DS	0002H
FTOV:	DS	0002H
FLABL:	DS	0002H
FOBJ:	DS	0002H
RWRK:	DS	0002H
OTOP	EQU	$
;
	END
