;***********************************************************
;SAMPLE 6_2_8_multiple
;***********************************************************
;例7：乗算
;2つの8ビット16進数の乗算
;因数はメモリ位置2100Hと2101Hにある、
;結果はメモリセル2102H（下位バイト）と2103H（上位バイト）に格納されます。
;***********************************************************
;
	ORG	2000H
EX08:
	LD	HL,2100H
	LD	C,(HL)
	INC	HL
	LD	D,(HL)
	CALL	MULT
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B
	HALT
				;8ビット乗算で 16ビットの結果 C x D = BC
MULT:
	SUB	A		;レジスタA削除
	LD	B,8		;8ビット
MULT1:
	RR	C		;乗算器のシフト
	JR	NC,MULT2	;CY = 0 なら加算なし
	ADD	A,D		;加算による結果
MULT2:
	RR	A		;CY の上位ビットと下位結果ビットをキャッチ
	DJNZ	MULT1
	RR	C		;REG.C の最後の結果ビット
	LD	B,A		;REG.B の高値バイト
	RET
;
	ORG	2100H
	DEFW	1F20H		;入力
	DEFW	0000H		;結果
	END
